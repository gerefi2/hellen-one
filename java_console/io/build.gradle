plugins {
    id 'java-library'
    id 'java-test-fixtures'
}

apply from: '../../android/dependencies.gradle'
apply from: '../../java_tools/dependencies.gradle'

configurations {
    all*.exclude group: 'org.hamcrest', module: 'hamcrest-core'
}

tasks.withType(Test) {
    systemProperty "java.library.path", "../../unit_tests/build"
}

dependencies {
    implementation global_libs.javaxJson
    api global_libs.jcip
    api project(':models')
    api project(':peak-can-basic')
    api project(':core_io')
    api project(':logging')

    implementation group: 'com.fazecast', name: 'jSerialComm', version: '2.10.3'
    implementation group: 'tel.schich', name: 'javacan-core', version: "$javaCanVersion"
    implementation group: 'tel.schich', name: 'javacan-core', version: "$javaCanVersion", classifier: 'x86_64'
    implementation global_libs.json
    implementation ts_plugin_libs.httpclient

    testFixturesImplementation global_libs.mockito
    testFixturesApi global_libs.annotations
    testFixturesApi global_libs.junit5api
}

tasks.register('copyJniHeader', Copy) {
    dependsOn("compileJava")
    from 'build/generated/sources/headers/java/main'
    into "../../unit_tests/native"
    include 'com*.h'
}

test {
    dependsOn("copyJniHeader")
}
